if (!publishJars.toBoolean()) return

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.nemerosa.versioning'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    name project.name
    description 'VWorkflows'
    url 'http://vworkflows.mihosoft.eu'
    inceptionYear '2012'
    licenses {
        license([:]) {
            name 'BSD 2-Clause'
            url 'https://opensource.org/licenses/BSD-2-Clause'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/miho/VWorkflows'
    }
    developers {
        [
            mihosoft: 'Michael Hoffer'
        ].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                // all dependencies should use the default scope (compile) but
                // Gradle insists in using runtime as default
                asNode().dependencies.dependency.each { dep ->
                    if (dep.scope.text() == 'runtime') {
                        dep.remove(dep.scope)
                    }
                }
                asNode().appendNode 'description', 'VWorkflows library'
                asNode().children().last() + pomConfig
            }
        }
    }
}


jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
            'Build-Date': project.buildDate,
            'Build-Time': project.buildTime,
            'Build-Revision': versioning.info.commit,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
    metaInf {
        from rootProject.file('LICENSE.txt')
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = 'VWorkflows'
        userOrg = 'miho'
        name = project.name
        desc = project.name
        licenses = ['BSD 2-Clause']
        labels = ['javafx', 'workflows','vrl','vrl-studio', 'graphs','flows', 'visual programming']
        websiteUrl = 'http://vworkflows.mihosoft.eu'
        issueTrackerUrl = 'https://github.com/miho/VWorkflows/issues'
        vcsUrl = 'https://github.com/miho/VWorkflows.git'
        publicDownloadNumbers = true
    }
}
